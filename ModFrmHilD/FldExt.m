// save fundamental unit
declare attributes FldAlg:
  TotallyPositiveUnitsGroup,
  TotallyPositiveUnitsMap,
  TotallyPositiveUnitsGenerators,
  TotallyPositiveUnitsGeneratorsOrients,
  UnitsGenerators,
  ClassGroupReps,
  MarkedEmbedding,
  Extensions,
  Restrictions,
  MatrixRingHoms,
  UnitCharFieldsByWeight,
  MinDistBtwnRoots,
  CMAutomorphism,
  TotallyRealSubfield
  ;


/////////////////////// (Narrow) Class Group Representatives ////////////
// FIXME : Move narrow class group code from graded ring to this section

intrinsic ClassGroupReps(F::FldAlg) -> SeqEnum
  {Return ideal representatives for the class group}
  if not assigned F`ClassGroupReps then 
    C, mC := ClassGroup(F);
    Reps := [ mC(i) : i in C ];
    F`ClassGroupReps := Reps;
  end if;
  return F`ClassGroupReps;
end intrinsic;

intrinsic CoprimeNarrowRepresentative(I::RngOrdIdl, J::RngOrdIdl) -> RngOrdElt
{Find a totally positive field element a such that qI is an integral ideal coprime to J;
 I and J must be defined over the same maximal order.}

    K := NumberField(Order(I));
    q := CoprimeRepresentative(I, J);

    // Nothing to do if K is imaginary or we already chose a good element.
    if Signature(q) eq [1,1] or Discriminant(K) lt 0 then return q; end if;
    if Signature(q) eq [-1,-1] then return -q; end if;

    // Otherwise, we have chosen a bad element, so must correct the signs.
    z := Sqrt(K!Discriminant(Integers(K)));
    require Norm(z) lt 0 : "Chosen generator of quadratic field is totally positive.";
    assert IsIntegral(z);
    
    if Signature(z) ne Signature(q) then
	z := -z;
    end if;

    NJ := Norm(J);
    d := GCD(Integers() ! Norm(z), NJ);

    if d eq 1 then return z*q; end if;
    b := ExactQuotient(NJ, d);
    return (1 + b * z)*q;
end intrinsic;

/////////////////////// Units and totally positive units /////////////////

intrinsic Signature(a::RngOrdElt) -> SeqEnum
  {}
  R := Parent(a);
  return Signature(FieldOfFractions(R)!a);
end intrinsic;

intrinsic TotallyPositiveUnitsGroup(F::FldAlg) -> GrpAb, Map
  {return the group of totally positive units of the base as an abstract group and the map from abstract totally positive unit group into F^\times_>0}

  if not assigned F`TotallyPositiveUnitsGroup or not assigned F`TotallyPositiveUnitsMap then
    U, mp := UnitGroup(F);
    // Stupid function, the isomorphism mu_2 -> ZZ/2*ZZ
    hiota := function(u);
      if u eq -1 then
        return 1;
      else
        return 0;
      end if;
    end function;

    F := NumberField(F);
    UZd := AbelianGroup([2 : i in [1..Degree(F)]]);
    phi := hom<U -> UZd | [[hiota(Sign(Evaluate(mp(U.i), v))) : v in RealPlaces(F)] : i in [1..#Generators(U)]]>;
    K := Kernel(phi);
    F`TotallyPositiveUnitsGroup := K;
    F`TotallyPositiveUnitsMap := mp;
  end if;

  return F`TotallyPositiveUnitsGroup, F`TotallyPositiveUnitsMap;
end intrinsic;

intrinsic FundamentalUnit(F::FldNum) -> FldElt
  {The fundamental unit of F}
  K := QuadraticField(Discriminant(Integers(F)));
  b, phi := IsIsomorphic(K, F);
  return phi(FundamentalUnit(K));
end intrinsic;

// The algorithm for producing generators is nondeterministic, so we need to "orient" 
// our chosen generators to avoid randomness. This particular choice remains
// consistent with the existing behavior of FundamentalUnitTotPos
function orient(F, eps)
  v := InfinitePlaces(F)[1];
  return (Abs(Evaluate(eps, v)) lt 1) select eps else eps^-1;
end function;

intrinsic TotallyPositiveUnitsGenerators(F::FldNum) -> SeqEnum[RngOrdElt]
  {
    parameters:
      F: a number field
    returns:
      A sequence of elements of the ring of integers
      which generate the group of totally positive units.
  }

  if Degree(F) eq 1 then
    return [Integers(F)!1];
  end if;

  if not assigned F`TotallyPositiveUnitsGenerators then
    PU, mPU := TotallyPositiveUnitsGroup(F);
    tpugs_unorient := [Integers(F)!mPU(PU.i) : i in [1 .. #Generators(PU)]];

    v := InfinitePlaces(F)[1];
    F`TotallyPositiveUnitsGenerators := [];
    F`TotallyPositiveUnitsGeneratorsOrients := [];

    // The algorithm for producing generators is nondeterministic, so we need to "orient" 
    // our chosen generators to avoid randomness. This particular choice remains
    // consistent with the existing behavior of FundamentalUnitTotPos
    //
    // We keep track of which generators are inverted with respect to the 
    // Generators(F`TotallyPositiveUnitsGroup) because we need to solve the word
    // problem in the unit generators code and it solves it there with respect
    // to Generators(F`TotallyPositiveUnitsGroup).
    for eps in tpugs_unorient do
      if Evaluate(eps, v) lt 1 then
        Append(~F`TotallyPositiveUnitsGenerators, eps);
        Append(~F`TotallyPositiveUnitsGeneratorsOrients, 1);
      else
        Append(~F`TotallyPositiveUnitsGenerators, eps^-1);
        Append(~F`TotallyPositiveUnitsGeneratorsOrients, -1);
      end if;
    end for;
  end if;
  return F`TotallyPositiveUnitsGenerators;
end intrinsic;

intrinsic TotallyPositiveUnitsGeneratorsOrients(F::FldNum) -> SeqEnum
  {
    Returns a sequence whose ith entry e is such that the 
    ith element of SetToSequence(Generators(TotallyPositiveUnitsGroup)) is the
    ith element of F`TotallyPositiveUnitsGenerators raised to the eth power.

    Here, e will either be 1 or -1. 
  }
  _ := TotallyPositiveUnitsGenerators(F);
  return F`TotallyPositiveUnitsGeneratorsOrients;
end intrinsic;

intrinsic UnitsGenerators(F::FldNum : exclude_torsion:=true) -> SeqEnum[RngOrdElt]
  {
    parameters:
      F: a number field
    returns:
      A sequence of elements of the ring of integers
      which generate the group of units.

      If the field is CM, we return a root of unity followed by
      generators for the units in the totally real subfield.
  }
  if not assigned F`UnitsGenerators then
    U, mU := UnitGroup(F);
    require Order(U.1) gt 0 : "The first generator of the units group seems to no longer\
      be the generator of torsion, so you should update the code to find the generator\
      of torsion.";
    if not IsCM(F) then 
      ugs_unorient := [mU(U.i) : i in [1 .. #Generators(U)]];
      F`UnitsGenerators := [orient(F, eps) : eps in ugs_unorient];
    else
      _, _, Fplus := IsCM(F);
      if Fplus eq Rationals() then
        F`UnitsGenerators := [mU(U.1)];
      else
        F`UnitsGenerators := [mU(U.1)] cat [F!u : u in UnitsGenerators(Fplus)];
      end if;
      // orientation doesn't make sense as we are in a totally complex field anyways
    end if;

  end if;
  // this makes sense since we check earlier that U.1 is a generator for the torsion
  n := #F`UnitsGenerators;
  idxs := (exclude_torsion) select [2 .. n] else [1 .. n];
  if exclude_torsion then
    return F`UnitsGenerators[2 .. n];
  else
    return F`UnitsGenerators;
  end if;
end intrinsic;

/////////////////////// MarkedEmbedding and strong coercion ///////////////////////////

intrinsic MarkedEmbedding(K::FldNum) -> PlcNumElt
  {
    input:
      K: a number field
    returns:
      A distinguished infinite place of K.
      By default, this is the first infinite place.

    This function is here so that whenever
    we choose a distinguished place of K,
    we make the same choice. 
  }
  if assigned K`MarkedEmbedding then
    return K`MarkedEmbedding;
  end if;
  K`MarkedEmbedding := InfinitePlaces(K)[1];
  return K`MarkedEmbedding;
end intrinsic;

// Returns the minimum pairwise distance between the complex roots of f,
// or a harmless positive placeholder if f has fewer than two roots (in
// which case there is nothing to disambiguate).
function MinDistBtwnComplexRoots(f)
  Cx := ComplexField();
  roots := [tup[1] : tup in Roots(ChangeRing(f, Cx))];
  if #roots le 1 then
    return Cx!1;
  end if;
  min_dist := Abs(roots[1] - roots[2]);
  for i in [1 .. #roots] do
    for j in [i+1 .. #roots] do
      min_dist := Min(Abs(roots[i] - roots[j]), min_dist);
    end for;
  end for;
  return min_dist;
end function;

intrinsic IsStrongCoercible(L::Fld, x::.) -> BoolElt, FldElt
  {
    input:
      L - FldNum, FldQuad, FldCyc, or FldRat
      x - Any, but can return true only on a FldElt or RngElt
    returns:
      false if x cannot be coerced into L.
      true if x can be coerced into L, along with
        the strong coercion of x into L.
  }

  // Promote ring elements (e.g. RngIntElt, RngOrdElt) to their field of
  // fractions first, matching the docstring's promise to accept RngElt.
  if ISA(Type(x), RngElt) and not ISA(Type(x), FldElt) then
    ok, xf := IsCoercible(FieldOfFractions(Parent(x)), x);
    if not ok then
      return false, _;
    end if;
    return IsStrongCoercible(L, xf);
  end if;

  if not Type(x) in [FldNumElt, FldRatElt, FldQuadElt, FldCycElt] then
    return false, _;
  end if;

  if x in Rationals() then
    return true, L!x;
  end if;

  K := Parent(x);

  if L eq Rationals() then
    return false, _;
  end if;

  // We trust Magma's coercion if K and L have the same
  // defining polynomial
  if DefiningPolyCoeffs(K) eq DefiningPolyCoeffs(L) then
    if not IsIsomorphic(K, L) then
      return false, _;
    end if;
    return true, L!x;
  end if;

  // NB: we deliberately do NOT use Magma's built-in `IsCoercible(L, x)`/`L!x`
  // here. Those are only wired up automatically when L was literally
  // constructed as a tower over (or compositum containing) K; for two
  // independently constructed but abstractly isomorphic/related fields
  // (e.g. a cyclotomic splitting field and a totally real base field built
  // separately by the user, which is the common case in this codebase),
  // `IsCoercible`/`!` either report false or throw a hard runtime error
  // ("Arguments are not compatible") even though x is mathematically an
  // element of L. Instead we find x's own minimal polynomial over Q, look
  // for a root of it in L (which exists whenever x is really an element of
  // L, regardless of how K and L happen to have been constructed), and
  // disambiguate between the roots using the distinguished (marked) places
  // of K and L.
  // NB: we cache as a list of <L, x, r> triples, keyed on the actual
  // element x (compared with `eq`), rather than on x's minimal
  // polynomial. Two distinct (Galois-conjugate) elements of K can share
  // the exact same minimal polynomial while requiring different roots of
  // it in L, so caching by minimal polynomial alone would let whichever
  // conjugate got processed first silently poison the cache for the
  // other. We also cache by object identity of L (via IsIdentical), not
  // DefiningPolyCoeffs(L): two distinct Magma field objects (e.g. two
  // compositum fields built at different points of a computation) can
  // share the same defining polynomial without being the same object.
  if not assigned K`Extensions then
    K`Extensions := [* *];
  end if;

  for entry in K`Extensions do
    if IsIdentical(entry[1], L) and entry[2] eq x then
      return true, entry[3];
    end if;
  end for;

  f := MinimalPolynomial(x);
  rts := Roots(f, L);
  if #rts eq 0 then
    return false, _;
  end if;

  // Always verify against the marked embeddings, even when f has only
  // one root in L: if L does not contain the full Galois orbit of x
  // (e.g. L is a non-normal subfield), that single root may correspond
  // to a different conjugate of x rather than to x itself.
  v := MarkedEmbedding(K);
  w := MarkedEmbedding(L);
  x_eval := ComplexField()!Evaluate(x, v);
  tol := 0.5 * MinDistBtwnComplexRoots(f);
  for tup in rts do
    r := tup[1];
    if Abs(ComplexField()!Evaluate(r, w) - x_eval) lt tol then
      Append(~K`Extensions, <L, x, r>);
      return true, r;
    end if;
  end for;

  return false, _;
end intrinsic;

intrinsic StrongCoerce(L::Fld, x::RngElt) -> FldElt
  {
    input:
      L - FldNum, FldQuad, FldCyc, or FldRat
      x - An element of the ring of integers of one of the above
    returns:
      StrongCoerce applied to x coerced into its field of fractions.
  }

  K := FieldOfFractions(Parent(x));
  return StrongCoerce(L, K!x);
end intrinsic;

intrinsic StrongCoerce(L::Fld, x::FldElt) -> FldElt
  {
    input:
      L - FldNum, FldQuad, FldCyc, or FldRat
      x - An element of one of the above
    returns:
      Returns x as an element of L, such that evaluation at
      the distinguished place of the Parent of x is equal to
      evaluation of StrongCoerce(L, x) at the distinguished place
      of L.

    We find the root of the minimal polynomial of x which lies in L and
    agrees with x under the distinguished (marked) places of Parent(x)
    and L; see IsStrongCoercible for details and rationale.
  }

  require Type(x) in [FldNumElt, FldRatElt, FldQuadElt, FldCycElt] : "%o is not a valid type for strong coercion", Type(x);

  ok, y := IsStrongCoercible(L, x);
  require ok : "The element", x, "in", Parent(x), "cannot be strong coerced into", L;
  return y;
end intrinsic;

intrinsic ListToStrongCoercedSeq(A::List) -> SeqEnum
  {
    input:
      A - A list of number field elements
    returns:
      A sequence containing the elements of the list, strong coerced
      into a common parent field.
  }
  L := Rationals();
  for a in A do
    // in case a is a RngElt instead of a FldElt
    K := NumberField(Parent(a));
    L := (K eq L) select L else Compositum(L, K);
  end for;

  B := [];
  for a in A do
    Append(~B, StrongCoerce(L, a));
  end for;
  return B;
end intrinsic;

intrinsic StrongMultiply(A::List : K:=false) -> FldElt
  {
    input:
      A - A list of elements (strong) coercible into K, not necessarily
        from the same parent field.
      K - A field of type FldRat, FldCyc, FldNum, or FldQuad
    returns:
      The product of the elements in A, as an element of K.
  }

  // perform normal multiplication if all the objects 
  // are of the same type
  if &and[Parent(x) cmpeq Parent(A[1]) : x in A] then
    return &*[x : x in A];
  end if;
      
  // If K is not assigned, it should be the compositum
  // of all the elements
  if K cmpeq false then
    K := RationalsAsNumberField();
    for x in A do
      K := Compositum(K, NumberField(Parent(x)));
    end for;
  end if;

  prod := K!1;
  for x in A do
    y := (Type(x) in [FldRatElt, RngIntElt]) select x else StrongCoerce(K, x);
    prod *:= y;
  end for;
  return prod;
end intrinsic;

intrinsic StrongAdd(A::List : K:=false) -> FldElt
  {
    input:
      A - A list of elements (strong) coercible into K, not necessarily
        from the same parent field.
      K - A field of type FldRat, FldCyc, FldNum, or FldQuad
    returns:
      The sum of the elements in A, as an element of K.
  }

  // perform normal addition if all the objects 
  // are of the same type
  if &and[Parent(x) cmpeq Parent(A[1]) : x in A] then
    return &+[x : x in A];
  end if;

  // If K is not assigned, it should be the compositum
  // of all the elements
  if K cmpeq false then
    K := RationalsAsNumberField();
    for x in A do
      K := Compositum(K, NumberField(Parent(x)));
    end for;
  end if;

  sum := K!0;
  for x in A do
    y := (Type(x) in [FldRatElt, RngIntElt]) select x else StrongCoerce(K, x);
    sum +:= y;
  end for;
  return sum;
end intrinsic;

intrinsic StrongEquality(x::Any, y::Any : K:=false) -> BoolElt
  {
    Given elements x and y of possibly different fields,
    return true if and only if they are equal after
    strong embedding into their compositum.
  }
  if x cmpeq y then
    return true;
  end if;

  if K cmpeq false then
    K := Compositum(NumberField(Parent(x)), NumberField(Parent(y)));
  end if;

  return StrongCoerce(K, x) eq StrongCoerce(K, y);
end intrinsic;

intrinsic StrongCoerceMatrix(L::Fld, M::AlgMatElt) -> Mtrx
  {
    Strong coerces the matrix M, defined over a base field K,
    into the field L. 
  }
  R := Parent(M);
  K := BaseRing(R);
  require Type(K) in [FldRat, FldQuad, FldCyc, FldNum] : "M must be defined\
    over a field of type FldRat, FldQuad, FldCyc, or FldNum.";

  n := Nrows(M);
  // if either K or L is FldRat then automatic coercion should work.
  if K cmpeq Rationals() then
    return MatrixRing(L, n)!M;
  elif L cmpeq Rationals() then
    return MatrixRing(L, n)!M;
  end if;

  if DefiningPolyCoeffs(K) cmpeq DefiningPolyCoeffs(L) then
    return MatrixRing(L, n)!M;
  end if;

  if not assigned K`MatrixRingHoms then
    // M`MatrixRingHoms maps <L, n>, for L a number field
    // and n a positive integer, to a homomorphism from 
    // the ring of nxn matrices over K to the same over L.
    K`MatrixRingHoms := AssociativeArray();
  end if;


  S := MatrixRing(L, n);
  L_poly := DefiningPolyCoeffs(L);
  if IsSubfield(K, L) then
    if not IsDefined(K`MatrixRingHoms, <L_poly, n>) then
      ok, r := IsStrongCoercible(L, K.1);
      require ok : "Something's gone wrong, K.1 should be strong coercible into L.";
      phi := hom<K -> L | r>;
      K`MatrixRingHoms[<L_poly, n>] := hom<R -> S | phi>;
    end if;
    return K`MatrixRingHoms[<L_poly, n>](M);
  elif IsSubfield(L, K) then
    // The MatrixRingHoms logic only goes one way, so we strong coerce
    // the entries of the matrix individually if we are trying to 
    // coerce into a smaller subfield.
    
    // this will throw an error if the elements of M are not all coercible into L.
    return S![StrongCoerce(L, x) : x in Eltseq(M)];
  end if;
end intrinsic;

intrinsic MinDistBtwnRoots(K::FldAlg) -> FldReElt
  {
    Returns the minimum absolute value distance between 
    two roots of the defining polynomial of K.
  }
  if not assigned K`MinDistBtwnRoots then
    f := DefiningPolynomial(K);
    roots := [tup[1] : tup in Roots(ChangeRing(f, ComplexField()))];
    require #roots eq Degree(K) : "Something is wrong,\
      the multiplicity of every root should be one";
    require #roots gt 1 : "This shouldn't get called on the rationals";

    min_dist := Abs(roots[1] - roots[2]);
    for i in [1 .. #roots] do
      for j in [i+1 .. #roots] do
        min_dist := Min(Abs(roots[i] - roots[j]), min_dist);
      end for;
    end for;
    K`MinDistBtwnRoots := min_dist;
  end if;
  return K`MinDistBtwnRoots;
end intrinsic;

/////////////////////// coefficient ring ///////////////////////////

intrinsic NonSquareTotPosUnitsGens(F::FldNum) -> SeqEnum[RngOrdElt]
  {
    inputs:
      F: A totally real number field
    returns:
      The subsequence of TotallyPositiveUnitsGenerators(F) which 
      are not squares. These elements generate the group
      of totally positive units modulo the squares.
  }
  R<x> := PolynomialRing(F);
  for eps in TotallyPositiveUnitsGenerators(F) do
    out := [];
    if IsIrreducible(x^2-eps) then
      Append(~out, eps);
    end if;
  end for;
  return out;
end intrinsic;

intrinsic UnitCharField(F::FldNum, k::SeqEnum[RngIntElt]) -> FldNum
  {
    input:
      F: The base number field of the HMF.
      k: The weight of the HMF
    returns:
      The extension in which the output of the unit character lives.
  }
  return UnitCharFieldsByWeight(F, k);
end intrinsic;

intrinsic UnitCharFieldsByWeight(F::FldNum, k::SeqEnum[RngIntElt]) -> FldNum
  {
    input:
      F: The base number field of the HMF.
      k: The weight of the HMF
    returns:
      The extension in which the output of the unit character lives.

      If k is paritious, the output of the unit charactere
      lies in the splitting field of F.
      TODO abhijitm can do a little bit better
      than this (Phi \subseteq Spl(F) as Shimura calls it),
      but this is certainly true.

      If k is non-paritious, we return the compositum
      of the splitting field of F and
      the field generated by the polynomials
      x^2-eps_i for [eps_1, .., eps_(n-1)]
      a set of generators for the group of totally positive
      units of F.

      This is cached by (F, k) pair to reduce compatibility issues.
  }
  if assigned F`UnitCharFieldsByWeight then
    if IsDefined(F`UnitCharFieldsByWeight, k) then
      return F`UnitCharFieldsByWeight[k];
    end if;
  else
    F`UnitCharFieldsByWeight := AssociativeArray();
  end if;

  R<x> := PolynomialRing(F);
  if IsParallel(k) then
    // if the weight is parallel, the unit character is trivial
    L := Rationals();
  elif IsParitious(k) then
    L := SplittingField(F);
  else
    if NarrowClassNumber(F) eq 1 then
      L := SplittingField(F);
    else
      polys := [];
      for eps in NonSquareTotPosUnitsGens(F) do
        Append(~polys, x^2-eps);
      end for;
      K := (#polys eq 0) select F else AbsoluteField(ext<F | polys>);
      L := SplittingField(K);
    end if;
  end if;
  F`UnitCharFieldsByWeight[k] := L;
  return L;
end intrinsic;

/////////////////////// unit character ///////////////////////////

intrinsic AutsOfKReppingEmbeddingsOfF(F::FldNum, K::FldNum : Precision := 25) -> SeqEnum[Map]
  { 
    inputs:
      F: A number field of degree n
      K: A Galois number field containing the Galois closure of F.
    returns:
      We return a list [sigma_1, ..., sigma_n] 
      of automorphisms of K sorted such that if 
      [v_1, ..., v_n] is a list of embeddings of F, 
      then v_i(x) = v_0(sigma_i(x)) for all x in F. 
      Note that when F is not Galois, this list is
      not unique, but our algorithm is deterministic.
  }
  require IsSubfield(SplittingField(F), K) : "K must contain the Galois closure of F";
  require IsGalois(K) : "K needs to be a Galois field";
  n := Degree(F);
  a := PrimitiveElement(F);

  // a dictionary whose keys are embeddings of the primitive element of F
  // (or precisely, the first 10^Precision digits of its image under each
  // embedding). The value associated to the image under the ith embedding
  // is the integer i.
  a_embed_dict, f := PrimitiveEltEmbedDict(F : Precision:=Precision);
  
  // a distinguished place of K 
  // if we want to view our HMFs as having coefficients over C,
  // we should apply v_0 to all the coefficients
  v_0 := MarkedEmbedding(K);

  aut_dict := AssociativeArray();
  for aut in Automorphisms(K) do
    aut_a_est := f(Evaluate(aut(a), v_0));
    b, x := IsDefined(a_embed_dict, aut_a_est);
    if b then
      aut_dict[x] := aut;
      Remove(~a_embed_dict, aut_a_est);
      if #a_embed_dict eq 0 then
        break aut;
      end if;
    end if;
  end for;

  require #Keys(aut_dict) eq n : "Something's wrong, there should\
    be at one stored automorphism for each embedding of F";
  return [aut_dict[i] : i in [1 .. n]];
end intrinsic;

intrinsic PrimitiveEltEmbedDict(
    F::Fld :
    Precision:=100,
    f:=func<x|Round(10^Precision*x)>
    ) -> UserProgram, Assoc
  {
    input:
      F: A field with r real places and s complex places
    returns:
      - An associative array defining a map from images of the primitive element
      under archimedean embeddings of F to an index specifying the embedding. 
      - A function mapping a real number to the format of the keys
      of this associative array.
      
      
      First, list the r + 2s embeddings (not just the places) of F by writing
      down the r + s outputs of InfinitePlaces(F) and letting the last s be the conjugates
      in order of the s complex embeddings we already have. Then, the associative array
      takes the image of the primitive element of F under the jth embedding of the list
      to the integer j.
  }
  n := Degree(F);
  places := InfinitePlaces(F);
  a := PrimitiveElement(F);
  a_embed_dict := AssociativeArray();
  r, s := Signature(F);
  for i in [1 .. r] do
    z_i := f(Evaluate(a, places[i]));
    a_embed_dict[z_i] := i;
  end for;

  for i in [r+1 .. r+s] do
    z_i := f(Evaluate(a, places[i]));
    a_embed_dict[z_i] := i;
    a_embed_dict[Conjugate(z_i)] := i + s;
  end for;

  return a_embed_dict, f;
end intrinsic;

intrinsic AutsOfUCFReppingEmbeddingsOfF(F::FldNum, k::SeqEnum[RngIntElt] : Precision := 50) -> SeqEnum[Map]
  { 
    inputs:
      F: A real Galois number field of degree n
      k: A weight, given as a SeqEnum of n natural numbers
    returns:
      AutsOfKReppingEmbeddingsOfF applied with K equal to
      the unit character field associated to F and k.
  }
  K := UnitCharField(F, k);
  return AutsOfKReppingEmbeddingsOfF(F, K);
end intrinsic;

intrinsic ComplexConjugateOfPlace(w::PlcNumElt) -> FldElt
  {
    inputs:
      w: A complex place of a number field K.
    returns:
      An automorphism aut of K such that
      for any x in K, v_0(aut(x)) is the
      complex conjugate of v_0(x).
  }
  K := NumberField(w);
  require IsGalois(K) : "K is not Galois";
  require IsComplex(w) : "w is not a complex place";

  auts := AutsOfKReppingEmbeddingsOfF(K, K);
  w_idx := Index(w, auts);
  s := Integers()!(Degree(K) / 2);
  return auts[w_idx + s];
end intrinsic;

intrinsic EltToShiftedHalfWeight(x::FldElt, k::SeqEnum[RngIntElt]) -> FldElt
  {
    inputs: 
      x: A totally positive element of a number field F. 
         If k is nonparitious, we require x to be a totally
         positive unit. 
         // TODO abhijitm feels weird, will come back to it later,
         // but the point is that we won't actually run this in the
         // eigenvalue -> Fourier coefficient computation for
         // nonparitious (and maybe eventually all) weights,
         // so it only gets used to compute unit characters. 
      k: A weight
    returns:
      We want to compute the element
      y := \prod_i x_i^((k_0-k_i)/2)
      where x_i is the image of eps under the ith real embedding of F,
      k = (k_1, ..., k_n) is the weight, and k_0 = max_i(k_i). 

      This quantity appears in the computation of the unit character 
      as well as the computation of Fourier coefficients from Hecke eigenvalues.

      The element y will lie in the UnitCharField(F,k). 
  }
  assert IsTotallyPositive(x);
  if not IsParitious(k) then
    assert Norm(x) eq 1;
  end if;

  // extra NumberField to deal with when x is FldOrdElt
  F := NumberField(Parent(x));
  K := UnitCharField(F, k);
  k0 := Max(k);

  if IsParallel(k) then
    return K!1;
  end if;

  auts := AutsOfUCFReppingEmbeddingsOfF(F, k);
  if IsParitious(k) then
    // paritious nonparallel weight
    return &*[auts[i](K!x)^(ExactQuotient(k0 - k[i], 2)) : i in [1 .. #auts]];
  else
    // nonparitious weight
    v_0 := MarkedEmbedding(K);
    y := &*[auts[i](Sqrt(K!x))^(k0 - k[i]) : i in [1 .. #auts]];
    return PositiveInPlace(y, v_0);
  end if;
end intrinsic;

intrinsic PositiveInPlace(nu::FldNumElt, v::PlcNumElt) -> FldNumElt
  {
    input: 
      nu: An element of a number field F
      v: A place of F
    return:
      nu if v(nu) > 0 and -nu otherwise.
  }
  return (Evaluate(nu, v) gt 0) select nu else -1*nu;
end intrinsic;

intrinsic PositiveSqrt(nu::FldNumElt, K::FldNum) -> FldNumElt
  {
    input:
      nu: An element of a number field F
      K: A number field containing nu and a square root of nu.
    return:
      mu such that mu^2 = nu and mu is positive in the distinguished
      place of K.
  }
  mu := Sqrt(K!nu);
  v_0 := MarkedEmbedding(K);
  return (Evaluate(mu, v_0) ge 0) select mu else -1*mu;
end intrinsic;

intrinsic NormToHalfWeight(I::RngOrdFracIdl, k0::RngIntElt, K::FldNum) -> FldNumElt
  {
    input:
      I: A fractional ideal
      k0: A nonnegative integer
      K: A number field containing Norm(I)^(k0/2)
    return:
      Norm(I)^(k0/2)
  }
  Nm := K!Norm(I);
  return (k0 mod 2 eq 0) select Nm^(ExactQuotient(k0, 2)) else Nm^(k0/2);
end intrinsic;

intrinsic DefiningPolyCoeffs(K::Fld) -> SeqEnum
  {}
  if K eq Rationals() then
    K := RationalsAsNumberField();
  end if;
  return Coefficients(DefiningPolynomial(K));
end intrinsic;

intrinsic TraceBasis(aa::RngOrdFracIdl) -> SeqEnum
  {Given a fractional ideal aa, returns a basis (a_1, ..., a_n) in Smith normal form
   where Trace(a_1) > 0 and Trace(a_i) = 0 for i > 1. 
   Further, Trace(a_j*ZF.j) > 0 and Trace(a_i*ZF.j) = 0 for all i > j.}

  // Preliminaries
  B := Basis(aa);
  ZF := Parent(B[2]);
  // This also should be generalized appropriately when n > 2
  v := InfinitePlaces(NumberField(ZF))[2];

  // Change of basis
  traces := Matrix([[Integers()!Trace(B[i]*ZF.j) : j in [1..Degree(ZF)]] : i in [1..#B]]);
  _, Q := HermiteForm(traces);

  TB := Eltseq(Vector(B)*Transpose(ChangeRing(Q,ZF)));
  assert Trace(TB[1]) gt 0;
  assert &and[Trace(TB[i]) eq 0 : i in [2 .. #TB]];

  // Orienting the basis
  for i in [2 .. #TB] do
    if Evaluate(TB[i], v) lt 0 then
      TB[i] *:= -1;
    end if;
  end for;
  return TB;
end intrinsic;

intrinsic MinkowskiConstant(F::FldAlg) -> FldReElt
  {
    Returns the Minkowski constant of F.
  }
  s := #InfinitePlaces(F) - #RealPlaces(F);
  pi := Pi(RealField());
  D := Discriminant(Integers(F));
  n := Degree(F);
  return Sqrt(Abs(D)) * (4 / pi)^s * n^n / Factorial(n);
end intrinsic;

intrinsic LargestRootOfUnity(K::FldAlg) -> RngIntElt, FldElt
  {
    Given a number field K, returns the largest d such that 
    zeta_d lies in K.
  }
  zeta_d := UnitsGenerators(K : exclude_torsion:=false)[1];
  b, d := IsRootOfUnity(zeta_d);
  // the first generator should always be torsion
  assert b;
  return d, zeta_d;
end intrinsic;

intrinsic IsGalois(F::FldAlg) -> BoolElt
  {
    IsNormal fails if the defining polynomial of F has non-integral coefficients.
  }
  coeffs := DefiningPolyCoeffs(F);
  if &and[IsIntegral(a) : a in coeffs] and coeffs[#coeffs] eq 1 then
    return IsNormal(F);
  else
    return #GaloisGroup(F) eq Degree(F);
  end if;
end intrinsic;

intrinsic IsCM(K::FldAlg) -> BoolElt, Map, FldAlg
  {
    If K is CM, returns true, tau, where tau is complex conjugation in K
    Otherwise, returns false
  }
  if assigned K`CMAutomorphism then
    return true, K`CMAutomorphism, K`TotallyRealSubfield;
  end if;
  if not IsTotallyComplex(K) then
    return false, _, _;
  end if;

  auts := Automorphisms(K);
  if Degree(K) eq 2 then
    // we already checked that it's totally complex
    K`CMAutomorphism := auts[2];
    K`TotallyRealSubfield := Rationals();
    return true, K`CMAutomorphism, K`TotallyRealSubfield;
  end if;

  nontriv_auts := auts[2 .. #auts];
  for tup in Subfields(K, Integers()!(Degree(K) / 2)) do
    F := tup[1];
    if IsTotallyReal(F) then
      for aut in nontriv_auts do
        if aut(K!F.1) eq K!F.1 then
          K`CMAutomorphism := aut;
          K`TotallyRealSubfield := F;
          return true, aut, F;
        end if;
      end for;
      require 0 eq 1 : "Couldn't find an automorphism fixing an index 2
        totally real subfield of K, so something's gone wrong!";
    end if;
  end for;
  return false, _, _;
end intrinsic;
